plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version "${mdg_version}"
}

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.ALL
}


if (System.getenv('MOD_VERSION') != null) {
    mod_version = System.getenv('MOD_VERSION')
}
version = mod_version

base {
    archivesName = "${mod_id}-${minecraft_version}-neoforge"
}

//TODO: re enable source sets once mods are updated
sourceSets {
    main {
        java {
            exclude '**/AlmostUnifiedIntegrationImpl.java'
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}



neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
       accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')
    }


    runs {

        client {
            client()

            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
            programArgument "--username=Occultism"
        }

        server {
            server()

            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
            // programArgument '--nogui'
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
            jvmArguments.addAll('-XX:+AllowEnhancedClassRedefinition', '-XX:+IgnoreUnrecognizedVMOptions')
        }

    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    maven {
        url "https://dl.cloudsmith.io/public/klikli-dev/mods/maven/"
        content {
            includeGroup "com.klikli_dev"
        }
    }
    maven {
        name = "JEI and AlmostUnified"
        url = 'https://maven.blamejared.com/'
        content {
            includeGroup "mezz.jei"
            includeGroup "com.almostreliable.mods"
        }
    }
    maven {
        name = "OctoStudios (Curios Api Contiunation)"
        url = uri("https://maven.octo-studios.com/releases")
        content {
            includeGroup "top.theillusivec4.curios"
        }
    }
    maven {
        name = "Geckolib Maven"
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
        content {
            includeGroup "software.bernie.geckolib"
        }
    }
    maven {
        name 'LDTTeam - Modding - PerViamInvenire (MineColonies)'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
        content {
            includeGroup "com.ldtteam"
        }
    }
    maven {
        name = "SBL Maven"
        url "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
        content {
            includeGroup "net.tslat.smartbrainlib"
        }
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "TerraformersMC (emi)"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroup "dev.emi"
        }
    }
    flatDir {
        dirs 'lib'
    }
}

// use localRuntime for running mods locally
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    //curios
    compileOnly ("top.theillusivec4.curios:curios-neoforge:${curios_version}+${minecraft_version_major}:api") {transitive=false}
    localRuntime ("top.theillusivec4.curios:curios-neoforge:${curios_version}+${minecraft_version_major}") {transitive=false}

    //geckolib
    implementation ("software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}") {transitive=false}

    //smartbrainlib
    implementation ("net.tslat.smartbrainlib:SmartBrainLib-neoforge-${minecraft_version}:${smartbrainlib_version}") {transitive=false}

    //modonomicon
    implementation ("com.klikli_dev:modonomicon-${minecraft_version}-neoforge:${modonomicon_version}") {transitive=false}

    //Occultism
    implementation ("com.klikli_dev:occultism-${minecraft_version}-neoforge:${occultism_version}")

    // emi
    compileOnly ("dev.emi:emi-neoforge:${emi_version}:api") {transitive=false}
    localRuntime ("dev.emi:emi-neoforge:${emi_version}") {transitive=false}

    implementation "curse.maven:jade-324717:${jade_id}"
    implementation "curse.maven:dyenamics-509276:${dyenamics_id}"
}

processResources {
    def props = new HashMap<>(project.properties)
    def keys = new ArrayList<String>(props.keySet()) //to avoid concurrent modification exception

    for (String key : keys) {
        if (!(props.get(key) instanceof String) && !(props.get(key) instanceof Integer)) {
            props.remove(key)
        }
    }

    if (System.getenv('MOD_VERSION') != null) {
        props.put('mod_version', System.getenv('MOD_VERSION'))
    }

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand props
    }
    getInputs().properties(props)
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar { exclude ".cache" }


publishing {
    publications {
        mavenJava(MavenPublication) {
            /*
            artifactId base.archivesName.get()

            from components.java

            pom {
                name = mod_name
                description = 'A magic mod inspired by the world of Jonathan Stroud\'s Bartimaeus. With the help of occult rituals players can summon entities from the "Other Side" to perform magic.'
                url = 'https://github.com/klikli-dev/occultism'
                licenses {
                    license {
                        name = 'MIT AND CC-BY-4.0'
                        url = 'https://github.com/klikli-dev/occultism#licensing'
                    }
                }
                scm {
                    connection = 'scm:git:ssh:git@github.com:klikli-dev/occultism.git'
                    url = 'https://github.com/klikli-dev/occultism'
                }
            }
             */
        }
    }
    repositories {
        maven {
            /*
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/klikli-dev/mods/"
            def releasesRepoUrl = "https://maven.cloudsmith.io/klikli-dev/mods/"
            def snapshotsRepoUrl = "https://maven.cloudsmith.io/klikli-dev/mods/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_PASS')
            }
            */
        }
    }
}

//increase max errors from 100 to 2000
afterEvaluate {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "2000"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}